# vim: set filetype=python :

import os
import sys
from sys import stdout, stderr
import distutils
import distutils.sysconfig
import subprocess

Import('env')
ReadFileList=env['__readfunc']

# for the /bin directory
wlrun_file=env.File('wl-run.py')
multishear_run_file=env.File('multishear-run')
shear_run_file=env.File('shear-run')

bin_install_subdir = 'bin'
bin_targets = [wlrun_file, multishear_run_file, shear_run_file]


# for the /lib/pythonX/site-packages directory
init_file = env.File('__init__.py')
wlpipe_file = env.File('wlpipe.py')
oracle_file = env.File('oracle.py')
files_file = env.File('files.py')

main_libdir=distutils.sysconfig.get_python_lib()
pylib_install_subdir = main_libdir.replace(distutils.sysconfig.PREFIX+os.sep,'')
pylib_install_subdir = os.path.join(pylib_install_subdir, 'deswl')
pylib_targets = [init_file,wlpipe_file,oracle_file,files_file]

if env['WITH_PYTHON']:
    # try to build the python bindings
    try:

        files = ReadFileList('../src/wl_files.txt')
        files += ReadFileList('../src/wl_omp_files.txt')

        files = ['../src/'+f for f in files]

        
        import distutils.sysconfig
        env2 = env.Clone()
        env2.Prepend(CPPPATH=[distutils.sysconfig.get_python_inc()])
        env2.Prepend(SWIGFLAGS=['-python','-c++'])
        env2.Prepend(LIBS=['wl'])

        ext_lib = env2.SharedLibrary('_cwl', 
                                     ['cwl.i']+files+['cwl.cpp'],
                                     SHLIBPREFIX='')

        pylib_targets += [ext_lib,'cwl.py']

    except:
        stderr.write("Could not build python bindings")


if 'install' in COMMAND_LINE_TARGETS:
    RunInstall = env['_InstallProgram']
    RunInstall(env, bin_targets, bin_install_subdir)
    RunInstall(env, pylib_targets, pylib_install_subdir)

if 'uninstall' in COMMAND_LINE_TARGETS:
    RunUninstall = env['_UninstallProgram']
    RunUninstall(env, bin_targets, bin_install_subdir)

    # remove the directory too
    pylib_deltargets = pylib_targets + ['']
    RunUninstall(env, pylib_deltargets, pylib_install_subdir)


